extends Node2D

@onready var indicator = $"../UIMahouMeter"
@onready var indicator_mahou = $"../UIKoisuruMeter/KoisuruMeter"

const PIERCE_PROJECTILE_SCENE = preload("res://scenes/gameplay/projectiles/pierce_projectile.tscn")
const CHAIN_PROJECTILE_SCENE = preload("res://scenes/gameplay/projectiles/chain_projectile.tscn")

enum SkillShot {
	NONE,
	OVERDRIVE,
	PIERCE,
	CHAIN,
	NOVA
}

var queued_skill: SkillShot = SkillShot.NONE

const SKILL_DELAY_OVERDRIVE := 0.28
const SKILL_PIERCE_SPLIT_RANGE := 1500.0
const SKILL_PIERCE_PROJECTILE_SPEED := 1500.0
const SKILL_PIERCE_PROJECTILE_RADIUS := 62.0
const SKILL_CHAIN_MARK_RADIUS := 240.0
const SKILL_CHAIN_EXPLOSION_RADIUS := 220.0
const SKILL_CHAIN_PROJECTILE_SPEED := 900.0
const SKILL_CHAIN_PROJECTILE_SCALE := Vector2(1, 1) * 0.1
const SKILL_CHAIN_TARGET_SEARCH_RADIUS := 760.0
const SKILL_CHAIN_MAX_DEPTH_DIFFERENCE := 0.35
const SKILL_NOVA_PULL_RADIUS := 250.0
const SKILL_NOVA_PULL_SPEED := 700.0
const SKILL_NOVA_PULL_DURATION := 1
const SKILL_NOVA_CENTER_KNOCKBACK_DEPTH := 0.115
const SKILL_NOVA_CENTER_KNOCKBACK_SPEED := 1.5
const SKILL_NOVA_CENTER_KNOCKBACK_DURATION := 1
const SKILL_NOVA_SLOW_DURATION := 1
const SKILL_NOVA_SLOW_FACTOR := 0.35
const SKILL_AUTO_TARGET_RADIUS := 200.0

enum CharacterMode {
	CHAR_BAKU,
	CHAR_YUNA
}

var current_mode := CharacterMode.CHAR_BAKU

enum State {
	IDLE,
	SHOOT
}

enum AimState {
	NONE,
	ENEMY,
	WEAKNESS
}

enum AimLock {
	NONE,
	ENEMY,
	WEAKNESS
}

var locked_aim := AimLock.NONE
var shoot_enabled: bool = true
var switch_locked: bool = false
@export_range(0.02, 0.3, 0.01, "suffix:s") var shoot_lock_flicker_interval: float = 0.08
var force_hidden: bool = false

var _shoot_lock_flicker_timer: float = 0.0
var _shoot_lock_flicker_visible: bool = true

var aim_state := AimState.NONE

var state := State.IDLE
@onready var sprite: AnimatedSprite2D = $Visual/AnimatedSprite2D
var _pierce_burst_token_counter: int = 0
var _pierce_burst_used_tokens: Dictionary = {}

func _ready():
	Input.set_mouse_mode(Input.MOUSE_MODE_HIDDEN)
	current_mode = CharacterMode.CHAR_BAKU
	state = State.IDLE
	if indicator.has_signal("skill_casted"):
		indicator.connect("skill_casted", Callable(self, "_on_skill_casted"))
	update_crosshair_visual()
	#current_mode = CharacterMode.CHAR_BAKU

func set_state(new_state: State):
	if state == new_state:
		return
	state = new_state

	match state:
		State.IDLE:
			#if current_mode == CharacterMode.CHAR_BAKU:
				#sprite.play("default_baku")
			#if current_mode == CharacterMode.CHAR_YUNA:
				#sprite.play("default_yuna")
			pass
		State.SHOOT:
			#if current_mode == CharacterMode.CHAR_BAKU:
				#sprite.play("shooting_baku")
			#if current_mode == CharacterMode.CHAR_YUNA:
				#sprite.play("shooting_yuna")
			pass

func _input(event):
	
	if event is InputEventKey and event.pressed and not event.echo:
		if event.keycode == KEY_SHIFT and not switch_locked:
			switch_character()
			
	if event is InputEventMouseButton and event.pressed and event.button_index == MOUSE_BUTTON_LEFT:
		if not shoot_enabled:
			return
		set_state(State.SHOOT)
		shoot()

func set_shoot_enabled(enabled: bool) -> void:
	shoot_enabled = enabled
	_shoot_lock_flicker_timer = 0.0
	_shoot_lock_flicker_visible = true
	if not force_hidden:
		visible = true

func set_force_hidden(hidden_state: bool) -> void:
	force_hidden = hidden_state
	if force_hidden:
		visible = false
		return

	if shoot_enabled:
		visible = true
	else:
		visible = _shoot_lock_flicker_visible
		
func switch_character():
	if switch_locked:
		return
	if current_mode == CharacterMode.CHAR_BAKU:
		current_mode = CharacterMode.CHAR_YUNA
	else:
		current_mode = CharacterMode.CHAR_BAKU

	update_crosshair_visual()

func set_switch_locked(locked: bool) -> void:
	switch_locked = locked

func set_character_mode(character_name: String) -> void:
	var normalized := character_name.to_lower()
	if normalized == "yuna":
		current_mode = CharacterMode.CHAR_YUNA
	else:
		current_mode = CharacterMode.CHAR_BAKU
	update_crosshair_visual()
	
func update_crosshair_visual():
	if state != State.IDLE:
		return
	update_aim_from_lock()

func _on_animated_sprite_2d_animation_finished():
	if state == State.SHOOT:
		set_state(State.IDLE)
		update_crosshair_visual()
		

func _process(delta):
	global_position = get_global_mouse_position()
	if force_hidden:
		if visible:
			visible = false
		return
	_update_shoot_lock_flicker(delta)
	
	# Only check aim target when level is actively running to prevent spurious aim changes during intro/spawn
	var is_level_running := true
	var level_controller = get_tree().current_scene
	if level_controller != null and is_instance_valid(level_controller):
		# Check if level_running property exists
		for prop in level_controller.get_property_list():
			if String(prop.get("name", "")) == "level_running":
				is_level_running = level_controller.get("level_running") == true
				break
	
	if is_level_running:
		check_aim_target()

func _update_shoot_lock_flicker(delta: float) -> void:
	if shoot_enabled:
		if not visible:
			visible = true
		return

	_shoot_lock_flicker_timer += delta
	if _shoot_lock_flicker_timer >= shoot_lock_flicker_interval:
		_shoot_lock_flicker_timer = 0.0
		_shoot_lock_flicker_visible = not _shoot_lock_flicker_visible
		visible = _shoot_lock_flicker_visible


@onready var hand_baku := $"../Player/BakuMahou/AnimatedSprite2D2"
@onready var hand_yuna := $"../Player/YunaMahou/AnimatedSprite2D"

func shoot():
	if not shoot_enabled:
		return
	set_state(State.SHOOT)
	match current_mode:
		CharacterMode.CHAR_BAKU:
			shoot_baku()
			
		CharacterMode.CHAR_YUNA:
			shoot_yuna()
			

func shoot_baku():
	sprite.play("shooting_baku")
	hand_baku.play("shooting")
	if queued_skill == SkillShot.NONE:
		_trigger_recoil_shake()
	
	var hitbox = get_enemy_under_cursor()
	if hitbox:
		var enemy = hitbox.get_parent()
		if queued_skill == SkillShot.NONE:
			# Check if this is a weakness point
			if hitbox.is_in_group("weak_point"):
				# For weakness points, go up the hierarchy: Area2D -> WeaknessPoint -> SetOfWeakness -> EnemyBoss
				var weak_parent = enemy.get_parent()  # SetOfWeakness
				var boss = weak_parent.get_parent() if weak_parent else null  # EnemyBoss
				if boss and is_instance_valid(boss) and boss.has_method("on_hit"):
					boss.on_hit(hitbox)
			else:
				# For regular enemies
				if enemy.has_method("is_marked") and enemy.is_marked():
					enemy.explode_mark(SKILL_CHAIN_EXPLOSION_RADIUS, 1)
				if enemy.has_method("on_hit"):
					enemy.on_hit(hitbox)
		else:
			apply_skill_shot(enemy, hitbox, get_global_mouse_position())
		
		# Update combo counter on hit
		_notify_combo_counter_hit("baku")
		
		if not _is_boss_enemy(enemy, hitbox):
			indicator.add_slot("baku")
			indicator_mahou.add_baku()
	else:
		# Notify combo counter on miss
		_notify_combo_counter_miss()

func shoot_yuna():
	sprite.play("shooting_yuna")
	hand_yuna.play("shooting")
	if queued_skill == SkillShot.NONE:
		_trigger_recoil_shake()

	var hitbox = get_enemy_under_cursor()
	if hitbox:
		var enemy = hitbox.get_parent()
		if queued_skill == SkillShot.NONE:
			# Check if this is a weakness point
			if hitbox.is_in_group("weak_point"):
				# For weakness points, go up the hierarchy: Area2D -> WeaknessPoint -> SetOfWeakness -> EnemyBoss
				var weak_parent = enemy.get_parent()  # SetOfWeakness
				var boss = weak_parent.get_parent() if weak_parent else null  # EnemyBoss
				if boss and is_instance_valid(boss) and boss.has_method("on_hit"):
					boss.on_hit(hitbox)
			else:
				# For regular enemies
				if enemy.has_method("is_marked") and enemy.is_marked():
					enemy.explode_mark(SKILL_CHAIN_EXPLOSION_RADIUS, 1)
				if enemy.has_method("on_hit"):
					enemy.on_hit(hitbox)
		else:
			apply_skill_shot(enemy, hitbox, get_global_mouse_position())
		
		# Update combo counter on hit
		_notify_combo_counter_hit("yuna")
		
		if not _is_boss_enemy(enemy, hitbox):
			indicator.add_slot("yuna")
			indicator_mahou.add_yuna()
	else:
		# Notify combo counter on miss
		_notify_combo_counter_miss()

func _is_boss_enemy(enemy: Node, hitbox: Node = null) -> bool:
	# cek dari weak_point naik ke boss
	if hitbox != null and hitbox.is_in_group("weak_point"):
		return true
	# cek langsung dari enemy
	if enemy != null and enemy.is_in_group("boss"):
		return true

	if enemy != null and enemy.has_method("force_weakened_state"):
		return true
	return false
	
func get_enemy_under_cursor():
	var space = get_world_2d().direct_space_state
	var query = PhysicsPointQueryParameters2D.new()
	query.position = get_global_mouse_position()
	query.collide_with_areas = true
	query.collision_mask = 1 << 1

	var result = space.intersect_point(query, 16)
	if result.is_empty():
		return null

	var best_any: Area2D = null
	var best_any_z := -INF
	var best_any_order := -INF

	for r in result:
		var collider: Variant = r.get("collider")
		if not (collider is Area2D):
			continue

		var hit_area := collider as Area2D
		var enemy := hit_area.get_parent()
		if enemy == null:
			continue
		if not _is_enemy_targetable(enemy):
			continue

		var z := _get_effective_z_index(enemy)
		var order := float(enemy.get_index())

		if z > best_any_z or (z == best_any_z and order > best_any_order):
			best_any_z = z
			best_any_order = order
			best_any = hit_area
	if best_any != null:
		return best_any
	
	return null

func _is_enemy_targetable(enemy: Node) -> bool:
	if enemy == null or not is_instance_valid(enemy):
		return false

	if enemy.has_method("is_targetable"):
		return enemy.call("is_targetable") == true

	if enemy.has_method("is_dead"):
		if enemy.call("is_dead") == true:
			return false

	for prop in enemy.get_property_list():
		var prop_name := String(prop.get("name", ""))
		if prop_name == "is_dead" and enemy.get("is_dead") == true:
			return false
		if prop_name == "hp" and int(enemy.get("hp")) <= 0:
			return false

	if _is_enemy_behind_background(enemy):
		return false

	return true

func _is_enemy_behind_background(enemy: Node) -> bool:
	# Only apply this rule to actual enemy nodes, not arbitrary helper nodes.
	var has_background_top_prop := false
	for prop in enemy.get_property_list():
		if String(prop.get("name", "")) == "background_top_z":
			has_background_top_prop = true
			break

	if not enemy.is_in_group("enemy_nodes") and not has_background_top_prop:
		return false

	if not (enemy is CanvasItem):
		return false

	var background_top_z := 30.0
	if has_background_top_prop:
		background_top_z = float(enemy.get("background_top_z"))

	return _get_effective_z_index(enemy) <= background_top_z

func _get_effective_z_index(node: Node) -> float:
	if not (node is CanvasItem):
		return 0.0

	var effective_z := 0.0
	var current: Node = node
	while current != null and current is CanvasItem:
		var item := current as CanvasItem
		effective_z += float(item.z_index)
		if not item.z_as_relative:
			break
		current = item.get_parent()

	return effective_z
	
# =========== Logic aim dan weakness

var aim_timer := 0.0
var pending_aim_state := AimState.NONE
const AIM_DELAY := 0.05
var exit_timer := 0.0
const AIM_EXIT_DELAY := 0.06

func check_aim_target():
	var space = get_world_2d().direct_space_state

	var query := PhysicsPointQueryParameters2D.new()
	query.position = get_global_mouse_position()
	query.collide_with_areas = true
	query.collision_mask = 1 << 1

	var results = space.intersect_point(query, 8)

	if results.is_empty():
		set_locked_aim(AimLock.NONE)
		return

	# ==========================
	# PILIH TARGET PALING DEPAN (matching get_enemy_under_cursor logic)
	# ==========================
	var best: Area2D = null
	var best_z := -INF
	var best_order := -INF

	for r in results:
		var collider = r.get("collider")
		if not (collider is Area2D):
			continue

		var hit_area := collider as Area2D
		var enemy := hit_area.get_parent()
		if enemy == null:
			continue
		if not _is_enemy_targetable(enemy):
			continue

		# Use same z-index calculation as get_enemy_under_cursor
		var z := _get_effective_z_index(enemy)
		var order := float(enemy.get_index())

		if z > best_z or (z == best_z and order > best_order):
			best_z = z
			best_order = order
			best = hit_area

	if best == null:
		set_locked_aim(AimLock.NONE)
		return

	# ==========================
	# PRIORITAS WEAKNESS DARI TARGET TERDEPAN
	# ==========================
	if best.is_in_group("weak_point"):
		set_locked_aim(AimLock.WEAKNESS)
	elif best.is_in_group("enemy"):
		set_locked_aim(AimLock.ENEMY)
	else:
		set_locked_aim(AimLock.NONE)
		
var unlock_timer := 0.0
const UNLOCK_DELAY := 0.08


func set_aim_state(new_state: AimState):
	if aim_state == new_state:
		return

	aim_state = new_state

	# Jangan ganggu animasi nembak
	if state != State.IDLE:
		return
					
func set_locked_aim(new_lock: AimLock):
	if locked_aim == AimLock.WEAKNESS and new_lock == AimLock.ENEMY:
		unlock_timer += get_process_delta_time()
		if unlock_timer < UNLOCK_DELAY:
			return
	else:
		unlock_timer = 0.0

	if new_lock == locked_aim:
		return

	locked_aim = new_lock
	update_aim_from_lock()

func update_aim_from_lock():
	if state != State.IDLE:
		return

	match current_mode:
		CharacterMode.CHAR_BAKU:
			match locked_aim:
				AimLock.NONE:
					play_anim_safe("default_baku")
				AimLock.ENEMY:
					play_anim_safe("aim_baku")
				AimLock.WEAKNESS:
					play_anim_safe("aim_baku")

		CharacterMode.CHAR_YUNA:
			match locked_aim:
				AimLock.NONE:
					play_anim_safe("default_yuna")
				AimLock.ENEMY:
					play_anim_safe("aim_yuna")
				AimLock.WEAKNESS:
					play_anim_safe("aim_yuna")
					
func play_anim_safe(anim: String):
	if sprite.animation == anim:
		return
	sprite.play(anim)


func _on_skill_casted(skill_name: String) -> void:
	match skill_name:
		"OVERDRIVE":
			queued_skill = SkillShot.OVERDRIVE
		"PIERCE":
			queued_skill = SkillShot.PIERCE
		"CHAIN":
			queued_skill = SkillShot.CHAIN
		"NOVA":
			queued_skill = SkillShot.NOVA
		_:
			queued_skill = SkillShot.NONE

	cast_skill_shot_now()


func cast_skill_shot_now() -> void:
	if queued_skill == SkillShot.NONE:
		return

	set_state(State.SHOOT)
	match current_mode:
		CharacterMode.CHAR_BAKU:
			sprite.play("shooting_baku")
			hand_baku.play("shooting")
		CharacterMode.CHAR_YUNA:
			sprite.play("shooting_yuna")
			hand_yuna.play("shooting")

	var hitbox = get_enemy_under_cursor()
	var enemy: Node = null
	if hitbox:
		enemy = hitbox.get_parent()
	elif queued_skill == SkillShot.OVERDRIVE:
		# OVERDRIVE must be cast directly on a hit target.
		queued_skill = SkillShot.NONE
		return
	elif queued_skill == SkillShot.NOVA:
		# NOVA must be cast directly on a hit target.
		queued_skill = SkillShot.NONE
		return
	elif queued_skill == SkillShot.CHAIN:
		enemy = find_nearest_enemy_to_position(get_global_mouse_position(), SKILL_CHAIN_TARGET_SEARCH_RADIUS)
	elif queued_skill != SkillShot.PIERCE and queued_skill != SkillShot.NOVA:
		enemy = find_nearest_enemy_to_cursor(SKILL_AUTO_TARGET_RADIUS)

	if enemy == null and queued_skill != SkillShot.PIERCE and queued_skill != SkillShot.NOVA:
		# Jangan konsumsi skill kalau benar-benar tidak ada target.
		queued_skill = SkillShot.NONE
		return

	var was_skill_executed := queued_skill != SkillShot.NONE
	apply_skill_shot(enemy, hitbox, get_global_mouse_position())
	
	# Trigger audio feedback for skill shot
	if was_skill_executed:
		var character_name := "baku" if current_mode == CharacterMode.CHAR_BAKU else "yuna"
		_notify_combo_counter_hit(character_name)


func apply_skill_shot(enemy: Node, hitbox: Node, cast_position: Vector2) -> void:
	var target_enemy := _resolve_skill_target(enemy, hitbox)

	if target_enemy == null and queued_skill != SkillShot.PIERCE and queued_skill != SkillShot.NOVA:
		return

	_trigger_recoil_shake()

	match queued_skill:
		SkillShot.OVERDRIVE:
			var overdrive_delay := SKILL_DELAY_OVERDRIVE
			if target_enemy != null and target_enemy.has_method("play_red3_effect"):
				overdrive_delay = target_enemy.play_red3_effect()
			if target_enemy != null and target_enemy.has_method("play_redhit_effect"):
					target_enemy.play_redhit_effect()
			# Trigger shield/hit feedback on boss before attempting instakill
			if target_enemy != null and _is_boss_enemy(target_enemy, hitbox):
				if target_enemy.has_method("on_hit"):
					target_enemy.on_hit(hitbox)
			elif target_enemy != null and target_enemy.has_method("instakill"):
				# Only instakill non-boss enemies
				target_enemy.instakill(overdrive_delay)
				var overdrive_heal := EnhancementManager.get_overdrive_heal_amount()
				if overdrive_heal > 0:
					_schedule_player_heal(overdrive_heal, overdrive_delay)
				if EnhancementManager.should_overdrive_fill_random_combo() and indicator.has_method("queue_random_combo_fill"):
					indicator.queue_random_combo_fill()

		SkillShot.PIERCE:
			var projectile_origin := cast_position
			if target_enemy != null:
				if target_enemy.has_method("play_redhit_effect"):
					target_enemy.play_redhit_effect()
				var pierce_damage := 1 + EnhancementManager.get_pierce_damage_bonus_for(target_enemy)
				_apply_skill_hit_or_damage(target_enemy, hitbox, pierce_damage)
				projectile_origin = target_enemy.global_position
			spawn_pierce_projectiles(projectile_origin, target_enemy, true)

		SkillShot.CHAIN:
			if target_enemy != null and target_enemy.has_method("play_bluehit_effect"):
					target_enemy.play_bluehit_effect()
			# Apply hit/damage to trigger boss shield before chain bounces
			if target_enemy != null:
				_apply_skill_hit_or_damage(target_enemy, hitbox, 1)
			apply_chain_mark(target_enemy)

		SkillShot.NOVA:
			var center_pos := cast_position
			var target_depth := 0.5
			var nova_slow_duration := EnhancementManager.get_nova_slow_duration(SKILL_NOVA_SLOW_DURATION)
			
			if target_enemy != null and not _is_boss_enemy(target_enemy, hitbox):
				_apply_nova_target_visual_modulate(target_enemy)
			
			if target_enemy != null and target_enemy.has_method("play_blue3_effect"):
					target_enemy.play_blue3_effect()
					 
					
			if target_enemy != null:
				_apply_skill_hit_or_damage(target_enemy, hitbox, 1)
				center_pos = target_enemy.global_position
				target_depth = _extract_enemy_depth_or_default(target_enemy, 0.5)
				
			
			apply_nova_pull(center_pos, target_enemy, target_depth, nova_slow_duration)

	queued_skill = SkillShot.NONE

func _trigger_recoil_shake() -> void:
	var player_node := get_node_or_null("../Player")
	if player_node == null or not is_instance_valid(player_node):
		return
	if player_node.has_method("trigger_recoil_shake"):
		player_node.trigger_recoil_shake()


func _schedule_player_heal(amount: int, delay: float) -> void:
	if amount <= 0:
		return

	var player_node := get_node_or_null("../Player")
	if player_node == null or not is_instance_valid(player_node):
		return
	if not player_node.has_method("heal"):
		return

	var tree := get_tree()
	if tree == null:
		return

	var wait_time := maxf(delay, 0.0)
	if wait_time <= 0.0:
		player_node.heal(amount)
		return

	tree.create_timer(wait_time).timeout.connect(func():
		if is_instance_valid(player_node):
			player_node.heal(amount)
	)

func _next_pierce_burst_token() -> int:
	_pierce_burst_token_counter += 1
	return _pierce_burst_token_counter

func _on_pierce_projectile_first_hit(enemy: Node, burst_token: int, allow_secondary_burst: bool) -> void:
	if not allow_secondary_burst:
		return
	if enemy == null or not is_instance_valid(enemy):
		return
	if not EnhancementManager.should_pierce_spawn_secondary_burst():
		return
	if _pierce_burst_used_tokens.get(burst_token, false):
		return

	_pierce_burst_used_tokens[burst_token] = true
	spawn_pierce_projectiles((enemy as Node2D).global_position, enemy, false)

func _apply_skill_hit_or_damage(enemy: Node, hitbox: Node, damage: int) -> void:
	if enemy == null or not is_instance_valid(enemy) or damage <= 0:
		return

	# Untuk boss, pakai on_hit(hitbox) agar perilaku shield/weakness tetap konsisten.
	if _is_boss_enemy(enemy, hitbox):
		if enemy.has_method("on_hit"):
			enemy.on_hit(hitbox)
			return

	# Jika target adalah orb, panggil apply_damage dengan flag untuk melewati aturan warna
	if enemy.is_in_group("orb_nodes") and enemy.has_method("apply_damage"):
		enemy.apply_damage(damage, true)
		return

	if enemy.has_method("apply_damage"):
		enemy.apply_damage(damage)

func _resolve_skill_target(enemy: Node, hitbox: Node) -> Node:
	if hitbox != null and is_instance_valid(hitbox) and hitbox.is_in_group("weak_point"):
		var weak_point := hitbox.get_parent()
		var weak_set := weak_point.get_parent() if weak_point != null else null
		var boss := weak_set.get_parent() if weak_set != null else null
		if boss != null and is_instance_valid(boss):
			return boss

	return enemy

func find_nearest_enemy_to_cursor(radius: float) -> Node:
	return find_nearest_enemy_to_position(get_global_mouse_position(), radius)


func find_nearest_enemy_to_position(
	center_pos: Vector2,
	radius: float = INF,
	excluded: Array[Node] = [],
	reference_depth: Variant = null,
	max_depth_difference: float = INF,
	prefer_slow: bool = false
) -> Node:
	var best_enemy: Node = null
	var best_dist: float = radius
	var best_slow_enemy: Node = null
	var best_slow_dist: float = radius

	var enemies: Array = get_tree().get_nodes_in_group("enemy_nodes")
	for enemy in enemies:
		if enemy == null or not is_instance_valid(enemy):
			continue
		if not _is_enemy_targetable(enemy):
			continue
		if excluded.has(enemy):
			continue
		if reference_depth != null and not is_inf(max_depth_difference):
			var enemy_depth := _extract_enemy_depth_or_default(enemy, float(reference_depth))
			if absf(enemy_depth - float(reference_depth)) > max_depth_difference:
				continue

		var dist: float = enemy.global_position.distance_to(center_pos)
		if prefer_slow and _is_enemy_slow(enemy):
			if dist <= best_slow_dist:
				best_slow_dist = dist
				best_slow_enemy = enemy
		if dist <= best_dist:
			best_dist = dist
			best_enemy = enemy

	if prefer_slow and best_slow_enemy != null:
		return best_slow_enemy

	return best_enemy


func spawn_pierce_projectiles(origin: Vector2, source_enemy: Node = null, allow_secondary_burst: bool = true) -> void:
	var split_dirs: Array[Vector2] = []
	var base_dir := Vector2.UP
	for i in range(5):
		split_dirs.append(base_dir.rotated(deg_to_rad(float(i) * 72.0)))

	var burst_token := _next_pierce_burst_token() if allow_secondary_burst else -1

	var source_depth := 0.5
	if source_enemy != null and is_instance_valid(source_enemy):
		for prop in source_enemy.get_property_list():
			if String(prop.get("name", "")) == "depth":
				source_depth = float(source_enemy.get("depth"))
				break

	for dir: Vector2 in split_dirs:
		var projectile := PIERCE_PROJECTILE_SCENE.instantiate()
		projectile.set("speed_max", SKILL_PIERCE_PROJECTILE_SPEED)
		projectile.set("hit_radius_max", SKILL_PIERCE_PROJECTILE_RADIUS)
		projectile.set("locked_z_index", source_enemy.z_index if source_enemy != null and is_instance_valid(source_enemy) and "z_index" in source_enemy else 1100)
		if allow_secondary_burst and projectile.has_signal("first_hit_enemy_reached"):
			projectile.first_hit_enemy_reached.connect(Callable(self, "_on_pierce_projectile_first_hit").bind(burst_token, allow_secondary_burst))
		get_tree().current_scene.add_child(projectile)
		projectile.setup(origin, dir, source_enemy, source_depth)
		projectile.max_distance = SKILL_PIERCE_SPLIT_RANGE


func _extract_enemy_depth_or_default(enemy: Node, fallback_depth: float = 0.5) -> float:
	if enemy == null or not is_instance_valid(enemy):
		return fallback_depth

	for prop in enemy.get_property_list():
		if String(prop.get("name", "")) == "depth":
			return float(enemy.get("depth"))

	return fallback_depth


func apply_chain_mark(center_enemy: Node) -> void:
	if center_enemy == null or not is_instance_valid(center_enemy):
		return

	_run_chain_mark_sequence(center_enemy)

const SKILL_CHAIN_MAX_BOUNCES: int = 5

func _run_chain_mark_sequence(start_enemy: Node) -> void:
	var visited: Array[Node] = []
	var current: Node = start_enemy
	var bounce_count: int = 0 
	var max_bounces := SKILL_CHAIN_MAX_BOUNCES + EnhancementManager.get_chain_extra_bounces()
	var prefer_slow := EnhancementManager.should_prioritize_slow_for_chain()
	
	if start_enemy.has_method("play_bluehit_effect"):
		start_enemy.play_bluehit_effect()
	if start_enemy.is_in_group("boss") and start_enemy.has_method("on_hit"):
		start_enemy.on_hit()
	elif start_enemy.has_method("apply_damage"):
		if start_enemy.is_in_group("orb_nodes"):
			start_enemy.apply_damage(1, true)
		else:
			start_enemy.apply_damage(1)

	var damage: int = 2
	
	while current != null and is_instance_valid(current):
		if visited.has(current):
			break
			
		if bounce_count >= max_bounces:
			break

		visited.append(current)
		
		var current_depth := _extract_enemy_depth_or_default(current, 0.5)
		var next_enemy := find_nearest_enemy_to_position(
			(current as Node2D).global_position,
			SKILL_CHAIN_TARGET_SEARCH_RADIUS,
			visited,
			current_depth,
			SKILL_CHAIN_MAX_DEPTH_DIFFERENCE,
			prefer_slow
		)
		if next_enemy == null or not is_instance_valid(next_enemy):
			break

		await _play_chain_projectile((current as Node2D).global_position, (next_enemy as Node2D).global_position, next_enemy )
		
		if next_enemy.is_in_group("boss") and next_enemy.has_method("on_hit"):
			next_enemy.on_hit()
		elif next_enemy.has_method("apply_damage"):
			if next_enemy.is_in_group("orb_nodes"):
				next_enemy.apply_damage(damage, true)
			else:
				next_enemy.apply_damage(damage)

		if EnhancementManager.should_chain_last_hit_insta_kill() and bounce_count + 1 >= max_bounces and next_enemy.has_method("instakill"):
			next_enemy.instakill(0.0)

		damage += 1
		
		if next_enemy.has_method("play_bluehit_effect"):
			next_enemy.play_bluehit_effect()

		bounce_count += 1  # ← tambah setiap lompat
		current = next_enemy

const ENEMY_MAX_SCALE: float = 0.3      # scale maksimal enemy
const PROJECTILE_MAX_SCALE: float = 0.5  # scale maksimal projectile chain

func _play_chain_projectile(start_pos: Vector2, end_pos: Vector2, target_enemy: Node = null) -> void:
	var root := get_tree().current_scene
	if root == null:
		return

	if CHAIN_PROJECTILE_SCENE == null:
		return

	var projectile := CHAIN_PROJECTILE_SCENE.instantiate()
	if projectile == null:
		return

	root.add_child(projectile)

	for prop in projectile.get_property_list():
		var prop_name := String(prop.get("name", ""))
		if prop_name == "speed":
			projectile.set("speed", SKILL_CHAIN_PROJECTILE_SPEED)
		elif prop_name == "projectile_scale":
			projectile.set("projectile_scale", SKILL_CHAIN_PROJECTILE_SCALE)
			
	if target_enemy != null and is_instance_valid(target_enemy) and projectile.has_method("set_interpolation_targets"):
		const ENEMY_MIN_SCALE: float = 0.06
		const ENEMY_MAX_SCALE: float = 0.3
		const PROJECTILE_MAX_SCALE: float = 0.4

		const PROJECTILE_MIN_SCALE: float = PROJECTILE_MAX_SCALE * (ENEMY_MIN_SCALE / ENEMY_MAX_SCALE)
		
		var enemy_scale := (target_enemy as Node2D).scale.x
		
		var ratio := clampf((enemy_scale - ENEMY_MIN_SCALE) / (ENEMY_MAX_SCALE - ENEMY_MIN_SCALE), 0.0, 1.0)
		var scale_factor := lerpf(PROJECTILE_MIN_SCALE, PROJECTILE_MAX_SCALE, ratio)
		var target_proj_scale := SKILL_CHAIN_PROJECTILE_SCALE * (scale_factor / SKILL_CHAIN_PROJECTILE_SCALE.x)
				
		var target_z := (target_enemy as Node2D).z_index + 1
		projectile.set_interpolation_targets(target_proj_scale, target_z)

	if projectile.has_method("play_between"):
		await projectile.play_between(start_pos, target_enemy as Node2D)


func apply_nova_pull(center_pos: Vector2, center_enemy: Node, target_depth: float, slow_duration: float = SKILL_NOVA_SLOW_DURATION) -> void:
	
	# Apply nova pull ke center_enemy juga
	var target_scale = center_enemy.scale if center_enemy != null and is_instance_valid(center_enemy) else Vector2.ONE
	var center_entered_trigger := _has_entered_trigger_zorder(center_enemy)
	if center_enemy != null and is_instance_valid(center_enemy):
		if center_enemy.has_method("apply_nova_center_knockback_then_pull_effect"):
			center_enemy.apply_nova_center_knockback_then_pull_effect(center_pos, target_depth, SKILL_NOVA_CENTER_KNOCKBACK_DEPTH, SKILL_NOVA_CENTER_KNOCKBACK_SPEED, SKILL_NOVA_CENTER_KNOCKBACK_DURATION, SKILL_NOVA_PULL_SPEED, SKILL_NOVA_PULL_DURATION, slow_duration, SKILL_NOVA_SLOW_FACTOR)
		elif center_enemy.has_method("apply_nova_pull_effect"):
			center_enemy.apply_nova_pull_effect(center_pos, target_depth, SKILL_NOVA_PULL_SPEED, SKILL_NOVA_PULL_DURATION)
			if center_enemy.has_method("apply_slow"):
				center_enemy.apply_slow(slow_duration, SKILL_NOVA_SLOW_FACTOR)
	var enemies: Array = get_tree().get_nodes_in_group("enemy_nodes")
	for enemy in enemies:
		if enemy == null or not is_instance_valid(enemy):
			continue
		if enemy == center_enemy:
			continue
			
		if enemy.is_in_group("boss"):
			continue

		if center_entered_trigger and not _has_entered_trigger_zorder(enemy):
			continue
		
		var scale_x_int = float(enemy.scale.x)
		var enemy_depth := _extract_enemy_depth_or_default(enemy, 0.5)
		var enemy_max_scale := 1.0
		if "max_scale" in enemy:
			enemy_max_scale = maxf(float(enemy.get("max_scale")), 0.001)
		var nova_knockback_target_depth = clampf(enemy_depth + maxf(SKILL_NOVA_CENTER_KNOCKBACK_DEPTH, 0.0), 0.0, 1.0)
		
		if enemy.global_position.distance_to(center_pos) <= SKILL_NOVA_PULL_RADIUS * (scale_x_int / enemy_max_scale):
			if (enemy.scale >= target_scale and enemy.scale <= target_scale + Vector2(0.3, 0.3)) or (enemy.scale <= target_scale and enemy.scale >= target_scale - Vector2(0.1, 0.1)):
				if enemy.has_method("apply_nova_pull_effect"):
							enemy.apply_nova_pull_effect(center_pos, nova_knockback_target_depth , SKILL_NOVA_PULL_SPEED, SKILL_NOVA_PULL_DURATION)
				elif enemy.has_method("pull_towards"):
					if enemy.is_in_group("boss"):
						continue
					enemy.pull_towards(center_pos)
				if enemy.has_method("apply_slow"):
						enemy.apply_slow(slow_duration, SKILL_NOVA_SLOW_FACTOR)

func _is_enemy_slow(enemy: Node) -> bool:
	if enemy == null or not is_instance_valid(enemy):
		return false

	if enemy.has_method("is_slow"):
		return enemy.call("is_slow") == true

	for prop in enemy.get_property_list():
		if String(prop.get("name", "")) == "slow_timer":
			return float(enemy.get("slow_timer")) > 0.0

	return false


func _has_entered_trigger_zorder(node: Node) -> bool:
		if node == null or not is_instance_valid(node):
			return false
		if not node.has_method("get"):
			return false
		return node.get("has_entered_trigger_zorder") == true

func _apply_nova_target_visual_modulate(target_enemy: Node) -> void:
	if target_enemy == null or not is_instance_valid(target_enemy):
		return

	var target_visual: CanvasItem = null
	var visual_node := target_enemy.get_node_or_null("Visual/AnimatedSprite2D")
	if visual_node is CanvasItem:
		target_visual = visual_node as CanvasItem
	else:
		visual_node = target_enemy.get_node_or_null("AnimatedSprite2D")
		if visual_node is CanvasItem:
			target_visual = visual_node as CanvasItem

	if target_visual != null:
		target_visual.modulate = Color(0.622, 0.644, 1.0, 1.0)

func _notify_combo_counter_hit(character: String) -> void:
	"""Notify main level controller about a successful hit"""
	var level_controller = get_tree().current_scene
	if level_controller != null and is_instance_valid(level_controller):
		if level_controller.has_method("on_player_hit"):
			level_controller.on_player_hit(character)

func _notify_combo_counter_miss() -> void:
	"""Notify main level controller about a missed shot"""
	var level_controller = get_tree().current_scene
	if level_controller != null and is_instance_valid(level_controller):
		if level_controller.has_method("on_player_miss"):
			level_controller.on_player_miss()
